plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'dev.codedbydavid'
version = '0.0.1-SNAPSHOT'
description = 'EventHub REST API'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:2.0.3"
	}
}

/*
 * Integration Test source set + task (configs must exist before dependencies can use them)
 */
sourceSets {
	integrationTest {
		java.setSrcDirs(['src/integrationTest/java'])
		resources.setSrcDirs(['src/integrationTest/resources'])

		// IMPORTANT: make integrationTest see main classes/resources
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Testcontainers (aligned to BOM 2.0.3)
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
	testImplementation 'org.testcontainers:testcontainers-postgresql'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testRuntimeOnly 'com.h2database:h2'
	
	// H2 for local development (optional, can use PostgreSQL instead)
	runtimeOnly 'com.h2database:h2'

	// Integration Test deps (explicit to avoid missing test-autoconfigure on the integrationTest classpath)
	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Dependency for SB Actuator (Docker/Compose clean healthchecks)
	implementation "org.springframework.boot:spring-boot-starter-actuator"

	// Postgres driver
	runtimeOnly "org.postgresql:postgresql"

}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	useJUnitPlatform()
	shouldRunAfter tasks.test

	// Force Testcontainers to use Docker Desktop engine pipe on Windows
	//environment "DOCKER_HOST", "npipe:////./pipe/docker_engine"
	// Optional: if your environment blocks Ryuk (only enable if you still get init errors)
	// environment "TESTCONTAINERS_RYUK_DISABLED", "true"
}

tasks.check {
	dependsOn tasks.integrationTest
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('processIntegrationTestResources', Copy) {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}